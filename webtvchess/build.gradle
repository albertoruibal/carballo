apply plugin: 'java'
apply plugin: 'war'

archivesBaseName = 'webtvchess'

dependencies {
    compile 'com.google.gwt:gwt-user:2.5.1'
    compile 'com.google.gwt:gwt-dev:2.5.1'
    compile 'org.vectomatic:lib-gwt-svg:0.5.7'
    compile project(':core')
    compile project(path: ':core', configuration: 'archives')
    compile project(':gwt')
    compile project(path: ':gwt', configuration: 'archives')
}

task compileGwt (dependsOn: classes, type: JavaExec) {
    ext.buildDir = "${project.buildDir}/gwt"
    ext.extraDir = "${project.buildDir}/extra"

    inputs.source sourceSets.main.java.srcDirs
    inputs.dir sourceSets.main.output.resourcesDir
    outputs.dir buildDir

    doFirst {
        file(buildDir).mkdirs()
        //classpath.each { println it.name}
    }

    main = 'com.google.gwt.dev.Compiler'

    classpath {
        [
                sourceSets.main.java.srcDirs,           // Java source
                sourceSets.main.output.resourcesDir,    // Generated resources
                sourceSets.main.output.classesDir,      // Generated classes
                sourceSets.main.compileClasspath,       // Deps
        ]
    }

    args =
        [
                'org.vectomatic.libgwtsvgchess',
                '-war', buildDir,
                '-extra', extraDir,
                '-logLevel', 'INFO',
                '-localWorkers', '2',
                //'-optimize', '9',        // No effect?
                '-XdisableCastChecking',   // Disables runtime checking of cast operations. This can reduce compiled file sizes.
                '-XdisableClassMetadata',  // Disables some java.lang.Class methods. This can reduce compiled file sizes.
                //'-XenableClosureCompiler', // This should create smaller JavaScript output. Not compatible with the compile report
                '-compileReport',
                '-XsoycDetailed',
                '-XcompilerMetrics',
        ]

    maxHeapSize = '256M'
}

war.dependsOn compileGwt
war {
    from compileGwt.buildDir
}
war.doLast {
    ant.unzip(src: war.archivePath, dest: "./war")
}