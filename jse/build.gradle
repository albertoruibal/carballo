apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'carballo-jse'

dependencies {
    compile project(':core')
    testCompile 'junit:junit:4.12'
}

task proguard(type: proguard.gradle.ProGuardTask, dependsOn: 'jar') {
    doFirst {
        configurations.compile.each { println it.name }
    }
    ext {
        obfuscatedJar = "./carballo-${version}.jar"
    }
    injars jar.archivePath
    injars configurations.compile
    outjars obfuscatedJar
    libraryjars "${System.getProperty('java.home')}/lib/rt.jar"
    optimizationpasses 5
    allowaccessmodification
    dontskipnonpubliclibraryclassmembers
    target '1.7'
    keep 'public class com.alonsoruibal.chess.uci.Uci { *; }'
    keep 'public class EpdTest { *; }'
    doLast {
        logger.lifecycle "[Proguard] Generated obfuscated JAR in ${obfuscatedJar}"
    }
}

jar {
    manifest.attributes("Main-Class": "com.alonsoruibal.chess.uci.Uci")
}

test {
    ignoreFailures = true
    enableAssertions = false

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

task fastTest(type: Test) {
    ignoreFailures = true

    useJUnit {
        excludeCategories 'com.alonsoruibal.chess.SlowTest'
    }
}

publishing {
    publications {
        carballoJse(MavenPublication) {
            from components.java
            artifactId = 'carballo-jse'
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['carballoJse']
    pkg {
        repo = 'maven'
        name = 'com.alonsoruibal.carballo-jse'
        desc = 'Carballo (the galician word for Oak, it\'s all about search trees) is an Open Source Java chess engine'
        licenses = ['GPL-3.0']
        vcsUrl = 'https://github.com/albertoruibal/carballo.git'
        githubRepo = 'albertoruibal/carballo'
        publish = true
    }
}